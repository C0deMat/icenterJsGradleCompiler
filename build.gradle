plugins {
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'java'
    id 'java-gradle-plugin'
    id 'maven'
}

group 'com.leobia.gradle'
version '0.3.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

gradlePlugin {
    plugins {
        jsGradleCompiler {
            id = 'com.leobia.gradle.jsgradlecompiler'
            implementationClass = 'com.leobia.gradle.JsCompilerPlugin'
        }
    }
}

dependencies {
    compileOnly gradleApi()
    implementation ('com.google.auto.value:auto-value-annotations:1.7')
    implementation ('com.google.javascript:closure-compiler:v20200517') {
        exclude module: 'junit'
    }
    testImplementation ('org.junit.jupiter:junit-jupiter:5.6.2')
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('./repo'))
        }
    }
}

test {
    useJUnitPlatform()
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

pluginBundle {
    description = "Compile and minify .js files"
    website = "https://github.com/leobia/JsGradleCompiler"
    vcsUrl = "https://github.com/leobia/JsGradleCompiler"
    tags = ["js", "compile", "minify", "google closure", "transpile", "build", "build artifacts", "cd", "ci", "files", "java"]

    plugins {
        jsGradleCompiler {
            displayName = "Js Compiler and Minifier"
        }
    }
}

// Added to prevent: 'error: cannot access SourcePosition'
allprojects {
    tasks.withType(Javadoc).all { enabled = false }
}
